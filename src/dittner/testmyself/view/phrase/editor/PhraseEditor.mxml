<?xml version="1.0"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009"
         xmlns:s="library://ns.adobe.com/flex/spark"
         xmlns:audio="dittner.testmyself.view.common.audio.*"
         xmlns:editor="dittner.testmyself.view.common.editor.*"
         xmlns:input="dittner.testmyself.view.common.input.*"
         xmlns:list="dittner.testmyself.view.common.list.*"
         xmlns:button="dittner.testmyself.view.common.button.*"
         xmlns:common="dittner.testmyself.view.common.*"
         implements="dittner.testmyself.view.common.editor.IEditor"
         maxHeight.remove="300">

    <fx:Script><![CDATA[
        import dittner.testmyself.model.common.TransUnit;
        import dittner.testmyself.model.theme.Theme;
        import dittner.testmyself.view.common.renderer.AddedThemeItemRenderer;
        import dittner.testmyself.view.common.renderer.SeparatorThemeItemRenderer;
        import dittner.testmyself.view.common.renderer.ThemeItemRenderer;
        import dittner.testmyself.view.common.utils.AppColors;
        import dittner.testmyself.view.phrase.common.ThemeRendererData;

        import mx.collections.ArrayCollection;

        [Bindable]
        public var selectedPhrase:TransUnit;

        //--------------------------------------
        //  title
        //--------------------------------------
        private var _title:String = "";
        [Bindable("titleChanged")]
        public function get title():String {return _title;}
        public function set title(value:String):void {
            if (_title != value) {
                _title = value;
                dispatchEvent(new Event("titleChanged"));
            }
        }

        //--------------------------------------
        //  themes
        //--------------------------------------
        private var _themes:ArrayCollection;
        [Bindable("themesChanged")]
        public function get themes():ArrayCollection {return _themes;}
        public function set themes(value:ArrayCollection):void {
            if (_themes != value) {
                _themes = value;
                dispatchEvent(new Event("themesChanged"));
            }
        }

        //----------------------------------------------------------------------------------------------
        //
        //  Methods
        //
        //----------------------------------------------------------------------------------------------

        public function add():void {
            currentState = "add";
        }

        public function edit(vo:TransUnit):void {
            selectedPhrase = vo;
            currentState = "edit";
            if (originArea) originArea.text = vo.origin
            if (translationArea) translationArea.text = vo.translation;
        }

        public function remove(vo:TransUnit):void {
            selectedPhrase = vo;
            currentState = "remove";
        }

        public function close():void {
            if (isOpen) {
                clear();
                setCurrentState("normal");
            }
        }

        public function isOpen():Boolean {
            return currentState == "add" || currentState == "edit" || currentState == "remove";
        }

        private function clear():void {
            selectedPhrase = null;
            if (originArea) originArea.text = "";
            if (translationArea) translationArea.text = "";
            if (addThemeInput) addThemeInput.text = "";
            if (audioRecorder) {
                audioRecorder.stopRecording();
                audioRecorder.stopPlaying();
                audioRecorder.removeRecord();
            }
        }

        public function notifyInvalidData(msg:String):void {
            if (invalidNotifier) {
                invalidNotifier.text = msg;
                invalidNotifier.show();
            }
        }

        private static const HEADER_HEI:uint = EditorBg.HEADER_HEIGHT;
        private static const FOOTER_HEI:uint = 50;
        private static const PAD:uint = 20;
        private static const PAD_TOP:uint = 10;
        private static const HGAP:uint = 10;
        private static const VGAP:uint = 5;
        private static const THEMES_LIST_WID:uint = 250;
        private static const RECORDER_HEI:uint = 72;

        override protected function updateDisplayList(w:Number, h:Number):void {
            super.updateDisplayList(w, h);

            originArea.x = PAD;
            originArea.y = HEADER_HEI + PAD_TOP;
            originArea.height = (h - FOOTER_HEI - HEADER_HEI - PAD - PAD_TOP - 2 * VGAP - RECORDER_HEI) / 2;
            originArea.width = w - 2 * PAD - HGAP - THEMES_LIST_WID;

            translationArea.x = originArea.x;
            translationArea.y = originArea.y + originArea.height + VGAP;
            translationArea.height = originArea.height;
            translationArea.width = originArea.width;

            audioRecorder.x = PAD;
            audioRecorder.y = originArea.y + 2 * originArea.height + 2 * VGAP;
            audioRecorder.width = originArea.width;
            audioRecorder.height = RECORDER_HEI;

            themesList.x = translationArea.x + translationArea.width + HGAP;
            themesList.y = HEADER_HEI + PAD_TOP;
            themesList.height = 2 * originArea.height + VGAP;

            addThemeInput.x = themesList.x;
            addThemeInput.y = themesList.y + themesList.height + VGAP;
            addThemeInput.width = themesList.width - addThemeBtn.width + 1;

            addThemeBtn.x = addThemeInput.x + addThemeInput.width - 1;
            addThemeBtn.y = addThemeInput.y + 20;

            applyBtn.width = themesList.width;
            applyBtn.right = PAD;
            applyBtn.bottom = (FOOTER_HEI - applyBtn.height + EditorBg.BORDER_THICKNESS) / 2;

            cancelBtn.right = applyBtn.width + applyBtn.right + HGAP;
            cancelBtn.bottom = applyBtn.bottom;
            cancelBtn.width = applyBtn.width;
            cancelBtn.height = applyBtn.height;

            invalidNotifier.left = PAD;
            invalidNotifier.right = cancelBtn.width + cancelBtn.right + HGAP;
            invalidNotifier.bottom = applyBtn.bottom;
            invalidNotifier.height = applyBtn.height;

            removeTitleLbl.left = PAD;
            removeTitleLbl.right = PAD;
            removeTitleLbl.top = HEADER_HEI + PAD_TOP;

            removePhraseNameLbl.left = PAD;
            removePhraseNameLbl.right = PAD;
            removePhraseNameLbl.top = removeTitleLbl.y + removeTitleLbl.height + VGAP;
            removePhraseNameLbl.bottom = FOOTER_HEI + VGAP;
        }

        private function themesRendererFunc(item:Object):IFactory {
            var themeData:ThemeRendererData = item as ThemeRendererData;
            if (themeData) {
                if (themeData.isNew) return new ClassFactory(AddedThemeItemRenderer);
                else if (themeData.isSeparator) return new ClassFactory(SeparatorThemeItemRenderer);
                else return new ClassFactory(ThemeItemRenderer);
            }
            else throw new Error("Unknown theme item type:" + item.toString() + "!");
        }

        private static var themeRendererSeparator:ThemeRendererData;
        private var hasNewThemesSeparator:Boolean = false;
        private function addThemeBtnClickHandler(event:MouseEvent):void {
            if (!addThemeInput.text) return;

            var addedThemeName:String = addThemeInput.text;
            if (validateAddedTheme(addedThemeName)) {
                var vo:Theme = new Theme();
                vo.name = addedThemeName
                var addedTheme:ThemeRendererData = new ThemeRendererData(vo);
                addedTheme.isNew = true;
                addedTheme.selected = true;

                if (!hasNewThemesSeparator) {
                    hasNewThemesSeparator = true;

                    if (!themeRendererSeparator) {
                        themeRendererSeparator = new ThemeRendererData(new Theme());
                        themeRendererSeparator.isSeparator = true;
                    }
                    themes.addItem(themeRendererSeparator);
                }

                themes.addItem(addedTheme);
                addThemeInput.text = "";
            }
            else {
                notifyInvalidData('Не удалось добавить тему, так как тема с названием "' + addedThemeName + '" уже находится в списке!');
            }
        }

        private function validateAddedTheme(themeName:String):Boolean {
            for each(var themeData:ThemeRendererData in themes) {
                if (themeData.theme.name == themeName) return false;
            }
            return true;
        }

        private function applyBtnEnabled(audioRecording:Boolean):Boolean {
            return !audioRecording;
        }
        ]]></fx:Script>

    <s:states>
        <s:State name="disabled"/>
        <s:State name="normal"/>
        <s:State name="add"/>
        <s:State name="edit"/>
        <s:State name="remove"/>
    </s:states>


    <editor:EditorBg width="100%"
                     height="100%"
                     title="{title}"
                     mode.add="add"
                     mode.edit="edit"
                     mode.remove="remove"/>

    <s:Rect left="{EditorBg.BORDER_THICKNESS}"
            right="{EditorBg.BORDER_THICKNESS}"
            bottom="{EditorBg.BORDER_THICKNESS}"
            height="{FOOTER_HEI}">
        <s:fill>
            <s:SolidColor color="{AppColors.SCREEN_HEADER_BG}"/>
        </s:fill>
    </s:Rect>


    <!--ADD EDIT MODE-->

    <s:Group width="100%" height="100%"
             visible.add="true"
             visible.edit="true"
             visible="false">

        <input:TextAreaForm id="originArea"
                            title="Исходный текст"/>

        <input:TextAreaForm id="translationArea"
                            title="Перевод"/>


        <audio:AudioRecordPlayer id="audioRecorder"
                                 height="{RECORDER_HEI}"
                                 skinClass="dittner.testmyself.view.common.audio.skins.AudioRecordPlayerSkin"/>

        <list:ListForm id="themesList"
                       width="{THEMES_LIST_WID}"
                       dataProvider="{themes}"
                       title="Список тем"
                       itemRendererFunction="themesRendererFunc"
                       allowMultipleSelection="true">
            <list:layout>
                <s:VerticalLayout gap="0" useVirtualLayout="false"/>
            </list:layout>
        </list:ListForm>

        <input:TextInputForm id="addThemeInput"
                             title="Название новой темы"/>

        <button:BitmapButton id="addThemeBtn"
                             description="Создать новую тему"
                             upImage="@Embed(source='/assets/button/add_element_btn_up.png')"
                             downImage="@Embed(source='/assets/button/add_element_btn_down.png')"
                             click="addThemeBtnClickHandler(event)"/>

        <common:InvalidNotifier id="invalidNotifier"/>
    </s:Group>


    <button:CancelButton id="cancelBtn"
                         label="Отменить"/>

    <button:ApplyButton id="applyBtn"
                        label.add="Создать"
                        label.edit="Сохранить"
                        label.remove="Удалить"
                        enabled.remove="true"
                        enabled="{applyBtnEnabled(audioRecorder.recording)}"/>


    <!--REMOVE MODE-->

    <s:Label id="removeTitleLbl"
             visible.remove="true" visible="false"
             textAlign="center"
             color="{AppColors.TEXT_DARK}" fontSize="16"
             text="Вы действительно хотите безвозвратно удалить выбранный элемент?"/>

    <s:Label id="removePhraseNameLbl"
             visible.remove="true" visible="false"
             color="{AppColors.TEXT_DARK}" fontSize="20" fontWeight="bold"
             textAlign="center" verticalAlign="middle"
             text="{'«' + selectedPhrase.origin + '»'}"/>

</s:Group>
