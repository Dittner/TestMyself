<?xml version="1.0"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009"
         xmlns:s="library://ns.adobe.com/flex/spark"
         alpha="0">

    <fx:Script><![CDATA[
        import dittner.testmyself.utils.pendingInvoke.clearDelay;
        import dittner.testmyself.utils.pendingInvoke.doLaterInMSec;
        import dittner.testmyself.view.common.utils.AppColors;
        import dittner.testmyself.view.common.utils.Fonts;

        private static const PAD:uint = 2;

        [Bindable]
        public var text:String = "";

        private var daLaterOperationIndex:int;
        public function show():void {
            if (fadeEffect.isPlaying) fadeEffect.stop();
            alpha = 1;
            clearDelay(daLaterOperationIndex);
            daLaterOperationIndex = doLaterInMSec(hide, 5000)
        }

        public function hide():void {
            if (!fadeEffect.isPlaying) fadeEffect.play();
        }

        override protected function updateDisplayList(w:Number, h:Number):void {
            super.updateDisplayList(w, h);
            var g:Graphics = graphics;
            g.clear();

            if (w > 0 && h > 0) {
                var step:uint = 10;
                g.lineStyle(1, AppColors.TEXT_RED);
                for (var xi:uint = 2; xi < w - step / 2; xi += step) {
                    g.moveTo(xi, 0);
                    g.lineTo(xi + step / 2, 0);
                    g.moveTo(xi, h - 1);
                    g.lineTo(xi + step / 2, h - 1);
                }

                for (var yi:uint = 0; yi < h - step / 2; yi += step) {
                    g.moveTo(0, yi);
                    g.lineTo(0, yi + step / 2);
                    g.moveTo(w, yi);
                    g.lineTo(w, yi + step / 2);
                }
            }
        }
        ]]></fx:Script>

    <fx:Declarations>
        <s:Fade id="fadeEffect"
                alphaFrom="1"
                alphaTo="0"
                duration="5000"
                target="{this}"/>
    </fx:Declarations>

    <s:Label id="textDisplay"
             left="{PAD}" right="{PAD}" top="{PAD}" bottom="{PAD}"
             textAlign="center" verticalAlign="middle"
             text="{text}"
             fontFamily="{Fonts.VERDANA}"
             fontSize="12"
             maxDisplayedLines="2"
             color="{AppColors.TEXT_RED}"/>


</s:Group>
