<?xml version="1.0"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009"
         xmlns:s="library://ns.adobe.com/flex/spark"
         implements="dittner.testmyself.view.common.tooltip.IToolTip"
         addedToStage="addedToStageHandler(event)"
         removedFromStage="removedFromStageHandler(event)"
         mouseChildren="false" mouseEnabled="false"
         maxWidth="250" minWidth="250">

    <fx:Script><![CDATA[
        import dittner.testmyself.view.common.utils.AppColors;
        import dittner.testmyself.view.common.utils.Fonts;

        import mx.core.FlexGlobals;

        //--------------------------------------
        //  text
        //--------------------------------------
        private var _text:String;
        [Bindable("textChanged")]
        public function get text():String {return _text;}
        public function set text(value:String):void {
            if (_text != value) {
                _text = value;
                dispatchEvent(new Event("textChanged"));
            }
        }

        private function get appStage():Stage {
            return FlexGlobals.topLevelApplication.stage;
        }

        private var globalBounds:Rectangle;
        public function orient(globalBounds:Rectangle):void {
            this.globalBounds = globalBounds;
            invalidateDisplayList();
        }

        override protected function updateDisplayList(w:Number, h:Number):void {
            super.updateDisplayList(w, h);

            if (!globalBounds) return;

            var arrowDirection:String;
            if (globalBounds.x < w + 5) {
                arrowDirection = ToolTipPos.LEFT;
            }
            else if (globalBounds.x > appStage.fullScreenWidth - 50) {
                arrowDirection = globalBounds.y < h ? ToolTipPos.TOP : ToolTipPos.RIGHT;
            }
            else {
                arrowDirection = globalBounds.y < appStage.fullScreenHeight / 2 ? ToolTipPos.TOP : ToolTipPos.BOTTOM;
            }

            setArrowDirection(arrowDirection);

            var arrowPos:Point = getArrowPosition(arrowDirection, w, h);
            if (arrowDirection == ToolTipPos.LEFT) {
                x = globalBounds.x + globalBounds.width + arrowPos.x + 5;
                y = globalBounds.y + globalBounds.height / 2 - arrowPos.y;
            }
            else if (arrowDirection == ToolTipPos.TOP) {
                x = globalBounds.x + globalBounds.width / 2 - arrowPos.x;
                y = globalBounds.y + globalBounds.height + arrowPos.y;
            }
            else if (arrowDirection == ToolTipPos.RIGHT) {
                x = globalBounds.x - arrowPos.x;
                y = globalBounds.y + globalBounds.height / 2 - arrowPos.y - 5;
            }
            else if (arrowDirection == ToolTipPos.BOTTOM) {
                x = globalBounds.x + globalBounds.width / 2 - arrowPos.x;
                y = globalBounds.y - arrowPos.y;
            }
        }

        //--------------------------------------
        //  position
        //--------------------------------------
        private var _arrowDirection:String;
        private function setArrowDirection(value:String):void {
            if (_arrowDirection != value) {
                _arrowDirection = value;
                switch (_arrowDirection) {
                    case ToolTipPos.TOP :
                        setCurrentState("topArrow");
                        break;
                    case ToolTipPos.BOTTOM :
                        setCurrentState("bottomArrow");
                        break;
                    case ToolTipPos.LEFT :
                        setCurrentState("leftArrow");
                        break;
                    case ToolTipPos.RIGHT :
                        setCurrentState("rightArrow");
                        break;
                    default :
                        setCurrentState("normal");
                        break;
                }
            }
        }

        //--------------------------------------
        //  arrowLocalPos
        //--------------------------------------
        private var arrowPos:Point = new Point();
        private function getArrowPosition(direction:String, w:Number, h:Number):Point {
            switch (direction) {
                case ToolTipPos.TOP :
                    arrowPos.x = w - 37;
                    arrowPos.y = 2;
                    break;
                case ToolTipPos.BOTTOM :
                    arrowPos.x = w - 37;
                    arrowPos.y = h - 2;
                    break;
                case ToolTipPos.LEFT :
                    arrowPos.x = 2;
                    arrowPos.y = 37;
                    break;
                case ToolTipPos.RIGHT :
                    arrowPos.x = w - 2;
                    arrowPos.y = 37;
                    break;
                default :
                    arrowPos.x = 0;
                    arrowPos.y = 0;
                    break;
            }
            return arrowPos;
        }

        private function addedToStageHandler(event:Event):void {
            fadeEffect.play();
        }

        private function removedFromStageHandler(event:Event):void {
            if (fadeEffect.isPlaying) {
                fadeEffect.stop();
                alpha = 0;
            }
        }
        ]]></fx:Script>

    <fx:Declarations>
        <s:Fade id="fadeEffect"
                alphaFrom="0"
                alphaTo="1"
                duration="500"
                target="{this}"/>
    </fx:Declarations>

    <s:states>
        <s:State name="disabled"/>
        <s:State name="normal"/>
        <s:State name="topArrow"/>
        <s:State name="bottomArrow"/>
        <s:State name="leftArrow"/>
        <s:State name="rightArrow"/>
    </s:states>

    <s:BitmapImage width="100%" height="100%"
                   smooth="true"
                   source.leftArrow="@Embed(source='/assets/tooltip/tooltip_left.png', scaleGridLeft='37', scaleGridRight='38', scaleGridTop='46', scaleGridBottom='47')"
                   source.topArrow="@Embed(source='/assets/tooltip/tooltip_top.png', scaleGridLeft='27', scaleGridRight='29', scaleGridTop='30', scaleGridBottom='31')"
                   source.rightArrow="@Embed(source='/assets/tooltip/tooltip_right.png', scaleGridLeft='37', scaleGridRight='38', scaleGridTop='48', scaleGridBottom='49')"
                   source.bottomArrow="@Embed(source='/assets/tooltip/tooltip_bottom.png', scaleGridLeft='26', scaleGridRight='27', scaleGridTop='30', scaleGridBottom='31')"
            />

    <s:Label left="25" right="25" top="25" bottom="25"
             verticalAlign="middle"
             textAlign="center"
             fontFamily="{Fonts.ROBOTO}"
             fontWeight="bold"
             fontSize="16"
             color="{AppColors.TEXT_WHITE}"
             text="{text}">
        <s:filters>
            <s:DropShadowFilter color="0" alpha="1" angle="-90" blurX="1" blurY="1" distance="1"/>
        </s:filters>
    </s:Label>
</s:Group>
