<?xml version="1.0"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009"
         xmlns:s="library://ns.adobe.com/flex/spark"
         xmlns:audio="dittner.testmyself.deutsch.view.common.audio.*"
         xmlns:editor="dittner.testmyself.deutsch.view.common.editor.*"
         xmlns:input="dittner.testmyself.deutsch.view.common.input.*"
         xmlns:list="dittner.testmyself.deutsch.view.common.list.*"
         xmlns:button="dittner.testmyself.deutsch.view.common.button.*"
         xmlns:listBox="dittner.testmyself.deutsch.view.note.common.articleList.*"
         xmlns:examplelist="dittner.testmyself.deutsch.view.note.common.examplesForm.*"
         xmlns:lettersbar="dittner.testmyself.deutsch.view.note.common.lettersBar.*"
         xmlns:common2="dittner.testmyself.deutsch.view.note.common.*"
         xmlns:verbinputsform="dittner.testmyself.deutsch.view.note.common.verbInputsForm.*"
         maxHeight.remove="300"
         initialize="initializeHandler(event)">

    <fx:Script><![CDATA[
        import dittner.testmyself.core.model.note.INote;
        import dittner.testmyself.core.model.theme.ITheme;
        import dittner.testmyself.deutsch.model.AppConfig;
        import dittner.testmyself.deutsch.model.domain.word.WordArticle;
        import dittner.testmyself.deutsch.view.common.renderer.AddedThemeItemRenderer;
        import dittner.testmyself.deutsch.view.common.renderer.ThemeItemRenderer;
        import dittner.testmyself.deutsch.view.common.utils.AppColors;
        import dittner.testmyself.deutsch.view.common.utils.AppSizes;
        import dittner.testmyself.deutsch.view.note.common.NoteUtils;
        import dittner.testmyself.deutsch.view.note.form.phrase.PhraseFormState;
        import dittner.testmyself.deutsch.view.note.form.verb.VerbFormState;
        import dittner.testmyself.deutsch.view.note.form.word.WordFormState;
        import dittner.testmyself.deutsch.view.note.lesson.form.LessonFormState;

        import mx.collections.ArrayCollection;
        import mx.events.FlexEvent;

        import spark.components.supportClasses.TextInputOperation;
        import spark.events.TextOperationEvent;

        //--------------------------------------
        //  title
        //--------------------------------------
        private var _title:String = "";
        [Bindable("titleChanged")]
        public function get title():String {return _title;}
        public function set title(value:String):void {
            if (_title != value) {
                _title = value;
                dispatchEvent(new Event("titleChanged"));
            }
        }

        //--------------------------------------
        //  themes
        //--------------------------------------
        private var _themes:ArrayCollection = new ArrayCollection();
        [Bindable("themesChanged")]
        public function get themes():ArrayCollection {return _themes;}
        public function set themes(value:ArrayCollection):void {
            if (_themes != value) {
                _themes = value;
                dispatchEvent(new Event("themesChanged"));
            }
        }

        //--------------------------------------
        // noteType
        //--------------------------------------
        private var noteTypeChanged:Boolean = false;
        private var _noteType:String;
        [Bindable("noteTypeChanged")]
        public function get noteType():String {return _noteType;}
        public function set noteType(value:String):void {
            if (_noteType != value) {
                _noteType = value;
                noteTypeChanged = true;
                invalidateProperties();
                dispatchEvent(new Event("noteTypeChanged"));
            }
        }

        //----------------------------------------------------------------------------------------------
        //
        //  Methods
        //
        //----------------------------------------------------------------------------------------------

        private var curFormState:NoteFormState;
        private var defaultFormState:NoteFormState;
        private var phraseFormState:NoteFormState;
        private var wordFormState:NoteFormState;
        private var verbFormState:NoteFormState;
        private var lessonFormState:NoteFormState;

        private function initializeHandler(event:FlexEvent):void {
            defaultFormState = new NoteFormState();
            phraseFormState = new PhraseFormState(this);
            wordFormState = new WordFormState(this);
            verbFormState = new VerbFormState(this);
            lessonFormState = new LessonFormState(this);

            curFormState = defaultFormState;
        }

        override protected function commitProperties():void {
            super.commitProperties()
            if (noteTypeChanged) {
                noteTypeChanged = false;
                curFormState.clear();
                switch (noteType) {
                    case NoteType.WORD :
                        curFormState = wordFormState;
                        break;
                    case NoteType.PHRASE :
                        curFormState = phraseFormState;
                        break;
                    case NoteType.VERB :
                        curFormState = verbFormState;
                        break;
                    case NoteType.LESSON :
                        curFormState = lessonFormState;
                        break;
                    default :
                        curFormState = defaultFormState;
                }
            }
        }

        public function add():void {
            currentState = "add";
            curFormState.add();
        }

        public function edit(note:INote):void {
            currentState = "edit";
            curFormState.edit(note);
        }

        public function remove(note:INote):void {
            currentState = "remove";
            curFormState.remove(note);
        }

        public function close():void {
            if (isOpen()) {
                curFormState.clear();
                setCurrentState("normal");
            }
        }

        public function isOpen():Boolean {
            return currentState == "add" || currentState == "edit" || currentState == "remove";
        }

        public function notifyInvalidData(msg:String):void {
            if (invalidNotifier) {
                invalidNotifier.text = msg;
                invalidNotifier.show();
            }
        }

        override protected function updateDisplayList(w:Number, h:Number):void {
            super.updateDisplayList(w, h);
            curFormState.updateLayout(w, h);
        }

        private function themesRendererFunc(item:Object):IFactory {
            var theme:ITheme = item as ITheme;
            if (theme.id == -1) return new ClassFactory(AddedThemeItemRenderer);
            else return new ClassFactory(ThemeItemRenderer);
        }

        private function applyBtnEnabled(audioRecording:Boolean):Boolean {
            return !audioRecording;
        }

        private function wordValidationHandler(event:TextOperationEvent):void {
            if (event.operation is TextInputOperation) {
                var regexp:RegExp = NoteUtils.LETTERS;
                var match:Object = regexp.exec((event.operation as TextInputOperation).text);
                if (match == null) event.preventDefault();
            }
        }
        ]]></fx:Script>

    <s:states>
        <s:State name="disabled"/>
        <s:State name="normal"/>
        <s:State name="add"/>
        <s:State name="edit"/>
        <s:State name="remove"/>
    </s:states>


    <editor:EditorBg width="100%"
                     height="100%"
                     title="{title}"
                     mode.add="add"
                     mode.edit="edit"
                     mode.remove="remove"/>

    <lettersbar:LettersBar right="20" top="5" visible.remove="false"/>

    <s:Rect left="{EditorBg.BORDER_THICKNESS}"
            right="{EditorBg.BORDER_THICKNESS}"
            bottom="{EditorBg.BORDER_THICKNESS}"
            height="{AppSizes.EDITOR_FOOTER_HEIGHT}">
        <s:fill>
            <s:SolidColor color="{AppColors.SCREEN_HEADER_BG}"/>
        </s:fill>
    </s:Rect>


    <!--ADD EDIT MODE-->

    <s:Group width="100%" height="100%"
             visible.add="true"
             visible.edit="true"
             visible="false">
        <listBox:ListBox id="articleBox" requireSelection="true"
                         width="75"
                         itemRenderer="dittner.testmyself.deutsch.view.note.common.articleList.ArticleItemRenderer"
                         skinClass="dittner.testmyself.deutsch.view.note.common.articleList.ListBoxSkin"
                         dataProvider="{new ArrayCollection(WordArticle.ARTICLES)}"/>

        <input:TextAreaForm id="titleArea"
                            title="Исходный текст"
                            autoCorrect="true"
                            maxChars="{AppConfig.MAX_PHRASE_TEXT_LENGTH}"/>

        <input:TextInputForm id="wordInput"
                             maxChars="{AppConfig.MAX_WORD_LENGTH}"
                             changing="wordValidationHandler(event)"
                             autoCorrect="true"
                             title="Исходное слово"/>

        <verbinputsform:VerbInputsForm id="verbInputsForm"/>

        <input:TextInputForm id="wordOptionsInput"
                             maxChars="{AppConfig.MAX_WORD_LENGTH}"
                             title="Дополнительные данные"/>

        <input:TextAreaForm id="descriptionArea"
                            maxChars="{AppConfig.MAX_PHRASE_TEXT_LENGTH}"
                            autoCapitalize="sentence"
                            autoCorrect="true"
                            title="Перевод"/>

        <examplelist:ExamplesForm id="examplesForm" mainForm="{this}"/>

        <audio:AudioRecorder id="audioRecorder"/>

        <list:ListForm id="themesList"
                       dataProvider="{themes}"
                       title="Список тем"
                       itemRendererFunction="themesRendererFunc"
                       allowMultipleSelection="true">
            <list:layout>
                <s:VerticalLayout gap="0" useVirtualLayout="false"/>
            </list:layout>
        </list:ListForm>

        <input:TextInputForm id="addThemeInput"
                             maxChars="{AppConfig.MAX_THEME_NAME_LENGTH}"
                             title="Название новой темы"/>

        <button:BitmapButton id="addThemeBtn"
                             description="Создать новую тему"
                             upImage="@Embed(source='/assets/button/add_element_btn_up.png')"
                             downImage="@Embed(source='/assets/button/add_element_btn_down.png')"/>

        <common2:InvalidNotifier id="invalidNotifier"/>
    </s:Group>


    <button:GrayButton id="cancelBtn"
                       label="Отменить"/>

    <button:BlackButton id="applyBtn"
                        label.add="Создать"
                        label.edit="Сохранить"
                        label.remove="Удалить"
                        enabled.remove="true"
                        enabled="{applyBtnEnabled(audioRecorder.isRecording)}"/>

    <!--REMOVE MODE-->

    <s:Label id="removeTitleLbl"
             visible.remove="true" visible="false"
             textAlign="center"
             color="{AppColors.TEXT_DARK}" fontSize="16"
             text="Вы действительно хотите безвозвратно удалить выбранную запись?"/>

    <s:Label id="removeNoteTitleLbl"
             visible.remove="true" visible="false"
             color="{AppColors.TEXT_DARK}" fontSize="20" fontWeight="bold"
             textAlign="center" verticalAlign="middle"/>

</s:Group>
