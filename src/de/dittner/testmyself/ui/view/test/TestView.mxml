<?xml version="1.0"?>
<view:SmartView xmlns:fx="http://ns.adobe.com/mxml/2009"
                xmlns:core="de.dittner.testmyself.ui.common.view.*"
                xmlns:s="library://ns.adobe.com/flex/spark"
                xmlns:testlist="de.dittner.testmyself.ui.view.test.testList.*"
                xmlns:presets="de.dittner.testmyself.ui.view.test.presets.*"
                xmlns:results="de.dittner.testmyself.ui.view.test.statistics.*"
                xmlns:testing="de.dittner.testmyself.ui.view.test.testing.*"
                currentState="inactive"
                xmlns:view="de.dittner.testmyself.ui.common.view.*">

    <fx:Script><![CDATA[
        import de.dittner.async.IAsyncOperation;
        import de.dittner.testmyself.model.domain.note.Note;
        import de.dittner.testmyself.ui.common.menu.ToolAction;
        import de.dittner.testmyself.ui.common.menu.ToolActionEvent;
        import de.dittner.testmyself.ui.common.view.ViewModelFactory;
        import de.dittner.testmyself.ui.view.noteList.components.form.FormOperationResult;

        [Bindable]
        public var vm:TestVM;

        override protected function activating():void {
            vm = ViewModelFactory.instance.testVM;
            vm.lockView();
            toolbar.hide();
            toolbar.revert();
        }

        override protected function activate():void {
            vm.unlockView();
            vm.viewActivated(viewID);
            showTestList();
            toolbar.addEventListener(ToolActionEvent.SELECTED, toolActionSelected);
        }

        override protected function deactivate():void {
            toolbar.removeEventListener(ToolActionEvent.SELECTED, toolActionSelected);
            setCurrentState("inactive");
            vm.viewDeactivated();
        }

        private function showTestList():void {
            vm.selectedTestTask = null;
            if (testListScreen) {
                testListScreen.activate();
            }
            if (testPresetsScreen) {
                testPresetsScreen.moveDirectionWhenHiding = currentState == "presets" ? "right" : "left";
                testPresetsScreen.deactivate();
            }
            setCurrentState("testList");
            toolbar.hide();
        }

        private function showTestPresets():void {
            vm.selectedTestTask = null;
            if (testListScreen) {
                testListScreen.deactivate();
            }
            if (statisticsScreen) {
                statisticsScreen.deactivate();
            }
            if (testingScreen) {
                testingScreen.deactivate();
            }
            if (testPresetsScreen) {
                testPresetsScreen.moveDirectionWhenHiding = currentState == "testList" ? "right" : "left";
                testPresetsScreen.activate();
            }
            setCurrentState("presets");
            toolbar.show();
            toolbar.revert();
            toolbar.goBackBtn.enabled = true;
        }

        private function showStatistics():void {
            vm.selectedTestTask = null;
            if (statisticsScreen) {
                statisticsScreen.activate(toolbar, actionMenu);
            }
            if (testPresetsScreen) {
                testPresetsScreen.moveDirectionWhenHiding = "left";
                testPresetsScreen.deactivate();
            }
            setCurrentState("statistics");
            toolbar.show();
            toolbar.revert();
            toolbar.goBackBtn.enabled = true;
            toolbar.showFailedTasksBtn.enabled = true;
            toolbar.showFailedTasksBtn.selected = vm.testPage.loadOnlyFailedTestTask;
        }

        private function startTesting():void {
            vm.selectedTestTask = null;
            if (testingScreen) {
                testingScreen.activate(actionMenu);
            }
            if (statisticsScreen) {
                statisticsScreen.deactivate();
            }
            if (testPresetsScreen) {
                testPresetsScreen.moveDirectionWhenHiding = "left";
                testPresetsScreen.deactivate();
            }
            setCurrentState("testing");
            toolbar.show();
            toolbar.revert();
            toolbar.goBackBtn.enabled = true;
        }

        private function toolActionSelected(event:ToolActionEvent):void {
            var op:IAsyncOperation;
            switch (event.actionID) {
                case(ToolAction.GO_BACK) :
                    if (currentState == "presets") {
                        showTestList();
                    }
                    else if (currentState == "statistics") {
                        if (statisticsScreen.noteContent.visible && statisticsScreen.noteContent.isNoteCardShown) {
                            statisticsScreen.noteContent.hideNoteCard();
                        }
                        else {
                            showTestPresets();
                        }
                    }
                    else if (currentState == "testing") {
                        testingScreen.deactivate();
                        showTestPresets();
                    }
                    break;
                case(ToolAction.EDIT_NOTE) :
                    form.visible = true;
                    op = form.edit(vm.selectedTestTask.note);
                    op.addCompleteCallback(noteEditOperationComplete);
                    break;
                case(ToolAction.REMOVE_NOTE) :
                    form.visible = true;
                    op = form.remove(vm.selectedTestTask.note);
                    op.addCompleteCallback(noteRemoveOperationComplete);
                    break;
                case(ToolAction.SHOW_FAILED_TASK) :
                    updateTestTaskList(true);
                    break;
                case(ToolAction.SHOW_ALL_TASK) :
                    updateTestTaskList(false);
                    break;
                case(ToolAction.SET_TASK_AS_RIGHT) :
                    if (vm.selectedTestTask && vm.selectedTestTask.isFailed) {
                        vm.selectedTestTask.isFailed = false;
                        vm.selectedTestTask.store();
                        vm.loadStatisticsPage();
                        statisticsScreen.noteContent.hideNoteCard();
                    }
                    break;
            }
        }

        private function noteRemoveOperationComplete(op:IAsyncOperation):void {
            if (op.isSuccess && op.result == FormOperationResult.OK) {
                if (currentState == "statistics") {
                    vm.testPage.allNotesAmount--;
                    vm.loadStatisticsPage();
                    statisticsScreen.noteContent.hideNoteCard();
                }
                else if (currentState == "testing") {
                    vm.loadNextTestTask();
                }
            }
            form.visible = false;
        }

        private function noteEditOperationComplete(op:IAsyncOperation):void {
            if (currentState == "statistics") {
                if (op.isSuccess && op.result == FormOperationResult.OK) {
                    vm.loadStatisticsPage();
                    statisticsScreen.noteContent.hideNoteCard();
                }
                else {
                    statisticsScreen.noteContent.updateSelectedItem();
                }
            }
            else if (currentState == "testing") {
                if (op.isSuccess && op.result == FormOperationResult.OK) {
                    vm.loadNextTestTask();
                }
            }
            form.visible = false;
        }

        private function updateTestTaskList(showOnlyFailed:Boolean):void {
            if (vm.testPage.loadOnlyFailedTestTask != showOnlyFailed) {
                vm.testPage.loadOnlyFailedTestTask = showOnlyFailed;
                vm.testPage.number = 0;
                vm.testPage.countAllNotes = true;
                vm.loadStatisticsPage();
                toolbar.setTaskAsRightBtn.enabled = vm.selectedTestTask && vm.selectedTestTask.isFailed;
                statisticsScreen.noteContent.hideNoteCard();
            }
        }

        //--------------------------------------
        //  selectedNote
        //--------------------------------------
        private var _selectedNote:Note;
        [Bindable("selectedNoteChanged")]
        public function get selectedNote():Note {return _selectedNote;}
        public function set selectedNote(value:Note):void {
            if (_selectedNote != value) {
                _selectedNote = value;

                toolbar.editNoteBtn.enabled = selectedNote;
                toolbar.removeNoteBtn.enabled = selectedNote;
                toolbar.setTaskAsRightBtn.enabled = currentState == "statistics" && vm.selectedTestTask && vm.selectedTestTask.isFailed;

                dispatchEvent(new Event("selectedNoteChanged"));
            }
        }
        ]]></fx:Script>
    <fx:Binding source="vm.selectedTestTask.note" destination="selectedNote"/>

    <core:states>
        <s:State name="inactive"/>
        <s:State name="normal"/>
        <s:State name="testList"/>
        <s:State name="presets"/>
        <s:State name="statistics"/>
        <s:State name="testing"/>
    </core:states>

    <!--content bg-->
    <s:Rect width="100%" height="100%">
        <s:fill>
            <s:BitmapFill source="@Embed(source='/assets/screen_bg_pattern.png')"
                          fillMode="repeat"/>
        </s:fill>
    </s:Rect>

    <testlist:TestListScreen id="testListScreen"
                             excludeFrom="inactive"
                             vm="{vm}"
                             width="{width}" height="100%"
                             padding="{PADDING}"
                             depth="1"
                             testSelected="showTestPresets()"/>

    <presets:TestPresetsScreen id="testPresetsScreen"
                               excludeFrom="inactive"
                               vm="{vm}"
                               width="{width}" height="100%"
                               padding="{PADDING}"
                               moveDirectionWhenHiding="right"
                               visible="false"
                               showStatistics="showStatistics()"
                               startTest="startTesting()"/>

    <testing:TestingScreen id="testingScreen"
                           excludeFrom="inactive"
                           vm="{vm}"
                           width="{width}" height="100%"
                           padding="{PADDING}"
                           moveDirectionWhenHiding="right"
                           visible="false"/>

    <results:StatisticsScreen id="statisticsScreen"
                              excludeFrom="inactive"
                              vm="{vm}"
                              width="{width}" height="100%"
                              padding="{PADDING}"
                              moveDirectionWhenHiding="right"
                              visible="false"/>

</view:SmartView>
