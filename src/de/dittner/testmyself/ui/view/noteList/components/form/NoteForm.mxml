<?xml version="1.0"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009"
         xmlns:s="library://ns.adobe.com/flex/spark"
         xmlns:audio="de.dittner.testmyself.ui.common.audio.*"
         xmlns:input="de.dittner.testmyself.ui.common.input.*"
         xmlns:list="de.dittner.testmyself.ui.common.list.*"
         xmlns:button="de.dittner.testmyself.ui.common.button.*"
         xmlns:listBox="de.dittner.testmyself.ui.view.noteList.components.form.articleList.*"
         xmlns:form2="de.dittner.testmyself.ui.view.noteList.components.form.*"
         currentState="normal">

    <fx:Script><![CDATA[
        import de.dittner.async.AsyncOperation;
        import de.dittner.async.IAsyncOperation;
        import de.dittner.async.utils.invalidateOf;
        import de.dittner.testmyself.model.Device;
        import de.dittner.testmyself.model.domain.note.Note;
        import de.dittner.testmyself.model.domain.note.verb.DeVerb;
        import de.dittner.testmyself.model.domain.note.word.DeWord;
        import de.dittner.testmyself.model.domain.note.word.DeWordArticle;
        import de.dittner.testmyself.model.domain.theme.Theme;
        import de.dittner.testmyself.model.domain.vocabulary.VocabularyID;
        import de.dittner.testmyself.ui.common.renderer.AddedThemeItemRenderer;
        import de.dittner.testmyself.ui.common.utils.AppColors;
        import de.dittner.testmyself.ui.common.utils.AppSizes;
        import de.dittner.testmyself.ui.common.utils.NoteFormUtils;
        import de.dittner.testmyself.ui.view.noteList.components.theme.ThemeItemRenderer;

        import mx.collections.ArrayCollection;

        import spark.components.supportClasses.TextInputOperation;
        import spark.events.TextOperationEvent;

        protected const HGAP:uint = 10;
        protected const VGAP:uint = 5;
        private var curOperation:IAsyncOperation;

        //--------------------------------------
        //  mode
        //--------------------------------------
        private var _mode:String = "";
        [Bindable("modeChanged")]
        public function get mode():String {return _mode;}
        private function setMode(value:String):void {
            if (_mode != value) {
                _mode = value;
                dispatchEvent(new Event("modeChanged"));
            }
        }

        //--------------------------------------
        //  note
        //--------------------------------------
        private var _note:Note;
        [Bindable("noteChanged")]
        public function get note():Note {return _note;}
        private function setNote(value:Note):void {
            if (_note != value) {
                _note = value;
                dispatchEvent(new Event("noteChanged"));
            }
        }

        //--------------------------------------
        // isCreateMode
        //--------------------------------------
        [Bindable("modeChanged")]
        public function get isAddMode():Boolean {return mode == "add";}

        //--------------------------------------
        // isEditMode
        //--------------------------------------
        [Bindable("modeChanged")]
        public function get isEditMode():Boolean {return mode == "edit";}

        //--------------------------------------
        // isRemoveMode
        //--------------------------------------
        [Bindable("modeChanged")]
        public function get isRemoveMode():Boolean {return mode == "remove";}

        //--------------------------------------
        //  title
        //--------------------------------------
        [Bindable("modeChanged")]
        public function get title():String {
            if (isAddMode) return "Hinzufügen";
            else if (isEditMode) return "Bearbeiten";
            else return "Entfernen";
        }

        //----------------------------------------------------------------------------------------------
        //
        //  Methods
        //
        //----------------------------------------------------------------------------------------------

        public function add(n:Note):IAsyncOperation {
            curOperation = new AsyncOperation();
            setNote(n);
            updateState();
            setMode("add");
            dispatchEvent(new Event("taskChanged"));
            invalidateOf(focusTextInput);
            return curOperation;
        }

        public function edit(n:Note):IAsyncOperation {
            curOperation = new AsyncOperation();
            setNote(n);
            updateState();
            setMode("edit");

            if (note is DeWord) {
                var word:DeWord = note as DeWord;
                articleBox.selectedItem = word.article;
                wordInput.text = word.title;
                wordOptionsInput.text = word.declension;
            }
            else if (note is DeVerb) {
                var verb:DeVerb = note as DeVerb;
                verbInputsForm.infinitive = verb.title;
                verbInputsForm.present = verb.present;
                verbInputsForm.past = verb.past;
                verbInputsForm.perfect = verb.perfect;
            }
            else {
                titleArea.text = note.title;
            }

            descriptionArea.text = note.description;
            audioRecorder.comment = note.audioComment;
            examplesForm.examples = new ArrayCollection(note.examples);
            themeList.dataProvider = note.vocabulary.themeColl;
            showSelectedThemes(note.themes);

            invalidateDisplayList();
            invalidateOf(focusTextInput);
            dispatchEvent(new Event("taskChanged"));
            return curOperation;
        }

        private function showSelectedThemes(noteThemes:Vector.<Theme>):void {
            if (noteThemes.length == 0 || !themeList.dataProvider || !themeList.dataProvider.length == 0) return;

            var isSelectedThemeHash:Object = {};
            var selectedItems:Vector.<Theme> = new Vector.<Theme>();
            for each(var noteTheme:Theme in noteThemes) isSelectedThemeHash[noteTheme.id] = true;
            for each(var theme:Theme in themeList.dataProvider)
                if (isSelectedThemeHash[theme.id]) selectedItems.push(theme);
            themeList.selectedItems = selectedItems;
        }

        public function remove(n:Note):IAsyncOperation {
            curOperation = new AsyncOperation();
            setNote(n);
            updateState();
            setMode("remove");
            removeNoteTitleLbl.text = '«' + note.title + '»';
            return curOperation;
        }

        private function updateState():void {
            if (note.vocabulary.id == VocabularyID.DE_WORD)
                currentState = "word";
            else if (note.vocabulary.id == VocabularyID.DE_VERB)
                currentState = "verb";
            else if (note.vocabulary.id == VocabularyID.DE_LESSON)
                currentState = "lesson";
            else
                currentState = "normal";
        }

        private function clear():void {
            articleBox.selectedItem = "";
            wordInput.text = "";
            wordOptionsInput.text = "";
            descriptionArea.text = "";
            addThemeInput.text = "";
            titleArea.text = "";
            audioRecorder.clear();
            examplesForm.clear();
            invalidNotifier.alpha = 0;
        }

        private function focusTextInput():void {
            if (stage && Device.isDesktop)
                stage.focus = currentState == "lesson" || currentState == "normal" ? titleArea.area : wordInput;
        }

        private function get isOpen():Boolean {
            return isAddMode || isEditMode || isRemoveMode;
        }

        private function notifyInvalidData(msg:String):void {
            if (invalidNotifier) {
                invalidNotifier.text = msg;
                invalidNotifier.show();
            }
        }

        private function cancel():void {
            if (isEditMode) {
                note.revertChanges();
            }
            curOperation.dispatchSuccess();
            close();
        }

        private function close():void {
            if (isOpen) {
                clear();
                setMode("");
                visible = false;
            }
        }

        private function apply():void {
            if (isAddMode || isEditMode) {
                correct();
                fillNote();
                var errKey:String = note.validate();
                if (errKey) {
                    notifyInvalidData(NoteValidationErrorKey.keyToString(errKey, note.vocabulary.langID));
                }
                else {
                    storeChanges();
                    curOperation.dispatchSuccess();
                    close();
                }
            }
            else {
                storeChanges();
                curOperation.dispatchSuccess();
                close();
            }
        }

        private function validateInputText():void {
            var errMsg:String = validateDuplicateNote();
            wordInput.isValidInput = !errMsg;
            titleArea.isValidInput = !errMsg;
            verbInputsForm.infinitiveInput.isValidInput = !errMsg;
        }

        private function validateDuplicateNote():String {
            var noteTitle:String = note is DeWord ? wordInput.text : note is DeVerb ? verbInputsForm.infinitiveInput.text : titleArea.text;
            if (!note.isExample && noteTitle && note.originalData.title != noteTitle && note.vocabulary.noteTitleHash.has(noteTitle))
                return NoteValidationErrorKey.NOTE_DUPLICATE;
        }

        private function correct():void {
            if (currentState == "word" || currentState == "verb") {
                titleArea.text = NoteFormUtils.correctText(titleArea.text);
                descriptionArea.text = NoteFormUtils.correctText(descriptionArea.text);
            }
            else {
                titleArea.text = NoteFormUtils.correctText(titleArea.text);
                wordOptionsInput.text = NoteFormUtils.correctText(wordOptionsInput.text);
                wordInput.text = NoteFormUtils.correctText(wordInput.text);
                descriptionArea.text = NoteFormUtils.formatDescriptionText(descriptionArea.text);
            }
        }

        private function fillNote():void {
            if (note is DeWord) {
                var word:DeWord = note as DeWord;
                word.article = articleBox.selectedItem;
                word.title = wordInput.text;
                word.description = descriptionArea.text;
                word.declension = wordOptionsInput.text;
                word.audioComment = audioRecorder.comment;
                word.examples = examplesForm.examples.source;
                word.themes = themeList.selectedItems as Vector.<Theme>;
            }
            else if (note is DeVerb) {
                var verb:DeVerb = note as DeVerb;
                verb.title = verbInputsForm.infinitiveInput.text;
                verb.description = descriptionArea.text;
                verb.audioComment = audioRecorder.comment;
                verb.present = verbInputsForm.presentInput.text;
                verb.past = verbInputsForm.pastInput.text;
                verb.perfect = verbInputsForm.perfectInput.text;
                verb.examples = examplesForm.examples.source;
                verb.themes = themeList.selectedItems as Vector.<Theme>;
            }
            else {
                note.title = titleArea.text;
                note.description = descriptionArea.text;
                note.audioComment = audioRecorder.comment;
            }
        }

        private function storeChanges():void {
            if (isAddMode || isEditMode)
                note.store();
            else if (isRemoveMode)
                note.remove();
        }

        private function themesRendererFunc(item:Object):IFactory {
            var theme:Theme = item as Theme;
            if (theme.id == -1) return new ClassFactory(AddedThemeItemRenderer);
            else return new ClassFactory(ThemeItemRenderer);
        }

        private function applyBtnEnabled(mode:String, audioRecording:Boolean):Boolean {
            return isRemoveMode || !audioRecording;
        }

        private function wordValidationHandler(event:TextOperationEvent):void {
            if (event.operation is TextInputOperation) {
                var inputOperation:TextInputOperation = event.operation as TextInputOperation;
                var regexp:RegExp = NoteFormUtils.LETTERS;
                var match:Object = regexp.exec(inputOperation.text);
                if (match == null) event.preventDefault();
            }
        }

        private function modeToApplyBtnTitle(mode:String):String {
            return isAddMode ? "Hinzufügen" : isEditMode ? "Speichern" : "Entfernen";
        }

        private function addThemeBtnClickHandler(event:MouseEvent):void {
            if (!addThemeInput.text) return;

            var theme:Theme = note.vocabulary.createTheme();
            theme.name = addThemeInput.text;
            var errKey:String = theme.validate();
            if (errKey) {
                notifyInvalidData(NoteValidationErrorKey.keyToString(errKey, note.vocabulary.langID));
            }
            else {
                var selectedItems:Vector.<Object> = themeList.selectedItems || new Vector.<Object>();
                selectedItems.push(theme);
                themeList.selectedItems = selectedItems;
                addThemeInput.text = "";
            }
        }
        ]]></fx:Script>

    <s:states>
        <s:State name="disabled"/>
        <s:State name="normal"/>
        <s:State name="word"/>
        <s:State name="verb"/>
        <s:State name="lesson"/>
    </s:states>

    <form2:EditorBg width="100%"
                    height="100%"
                    title="{title}"
                    mode="{mode}"/>

    <s:Rect left="{EditorBg.BORDER_THICKNESS}"
            right="{EditorBg.BORDER_THICKNESS}"
            bottom="{EditorBg.BORDER_THICKNESS}"
            height="{AppSizes.EDITOR_FOOTER_HEIGHT}">
        <s:fill>
            <s:SolidColor color="{AppColors.SCREEN_HEADER_BG}"/>
        </s:fill>
    </s:Rect>

    <button:WhiteButton id="formatBtn"
                        width="250" right="15" top="5"
                        label="Formatieren"
                        visible="{!isRemoveMode}"
                        click="correct()"/>

    <!--ADD EDIT MODE-->

    <s:HGroup width="100%" top="50" bottom="55" gap="{HGAP}" paddingLeft="15" paddingRight="15"
              visible="{!isRemoveMode}" includeInLayout="{!isRemoveMode}">
        <s:VGroup width="100%" height="100%" gap="{VGAP}">
            <s:HGroup width="100%" height="100%" gap="{HGAP}">
                <listBox:ListBox id="articleBox"
                                 verticalScrollPolicy="off"
                                 requireSelection="true"
                                 visible="false" includeInLayout="false"
                                 visible.word="true" includeInLayout.word="true"
                                 visible.verb="false" includeInLayout.verb="false"
                                 width="110"
                                 itemRenderer="de.dittner.testmyself.ui.view.noteList.components.form.articleList.ArticleItemRenderer"
                                 skinClass="de.dittner.testmyself.ui.view.noteList.components.form.articleList.ListBoxSkin"
                                 dataProvider="{new ArrayCollection(DeWordArticle.ARTICLES)}"/>

                <s:VGroup width="100%" height="100%" gap="{VGAP}">
                    <input:TextAreaForm id="titleArea"
                                        visible="true" includeInLayout="true"
                                        visible.word="false" includeInLayout.word="false"
                                        visible.verb="false" includeInLayout.verb="false"
                                        width="100%" height="100%"
                                        title="Deutschsatz"
                                        maxChars="{Device.MAX_TEXT_LENGTH}"
                                        change="validateInputText()"/>

                    <s:HGroup width="100%" gap="{HGAP}">
                        <input:TextInputForm id="wordInput"
                                             visible="false" includeInLayout="false"
                                             visible.word="true" includeInLayout.word="true"
                                             width="50%"
                                             maxChars="{Device.MAX_WORD_LENGTH}"
                                             changing="wordValidationHandler(event)"
                                             change="validateInputText()"
                                             title="Deutschtwort"/>

                        <input:TextInputForm id="wordOptionsInput"
                                             visible="false" includeInLayout="false"
                                             visible.word="true" includeInLayout.word="true"
                                             width="50%"
                                             maxChars="{Device.MAX_WORD_LENGTH}"
                                             title="Zusätzliche Daten"/>
                    </s:HGroup>

                    <s:HGroup width="100%" height="100%" gap="{HGAP}">
                        <form2:VerbInputsForm id="verbInputsForm"
                                              width="100%" height="100%"
                                              visible="false" includeInLayout="false"
                                              visible.word="false" includeInLayout.word="false"
                                              visible.verb="true" includeInLayout.verb="true"
                                              change="validateInputText()"/>

                        <input:TextAreaForm id="descriptionArea"
                                            width="100%" height="100%"
                                            maxChars="{Device.MAX_TEXT_LENGTH}"
                                            title="Übersetzung"/>
                    </s:HGroup>

                </s:VGroup>

            </s:HGroup>

            <form2:ExamplesForm id="examplesForm"
                                parentNote="{note}"
                                width="100%" height="100%"
                                visible="false" includeInLayout="false"
                                visible.word="true" includeInLayout.word="true"
                                visible.verb="true" includeInLayout.verb="true"
                                mainForm="{this}"/>

            <s:Group width="100%" height="72">
                <audio:AudioRecorder id="audioRecorder"
                                     height="72" left="118" left.lesson="0" left.normal="0" right="0"/>
            </s:Group>
        </s:VGroup>

        <s:VGroup width="250" height="100%" gap="{VGAP}"
                  visible.normal="false" includeInLayout.normal="false"
                  visible.lesson="{!isAddMode}" includeInLayout.lesson="{!isAddMode}"
                  visible.word="true" includeInLayout.word="true"
                  visible.verb="true" includeInLayout.verb="true">
            <list:ListForm id="themeList"
                           width="100%" height="100%"
                           title="Themenliste"
                           itemRendererFunction="themesRendererFunc"
                           allowMultipleSelection.lesson="false"
                           allowMultipleSelection="true">
                <list:layout>
                    <s:VerticalLayout gap="0" useVirtualLayout="false"/>
                </list:layout>
            </list:ListForm>

            <s:HGroup width="100%" height="72" gap="0" verticalAlign="bottom"
                      visible.lesson="false" includeInLayout.lesson="false">
                <input:TextInputForm id="addThemeInput"
                                     width="100%"
                                     maxChars="{Device.MAX_THEME_NAME_LENGTH}"
                                     title="Neuer Themaname"/>

                <button:BitmapButton id="addThemeBtn"
                                     description="Neues Thema hinzufügen"
                                     upImage="@Embed(source='/assets/button/add_element_btn_up.png')"
                                     downImage="@Embed(source='/assets/button/add_element_btn_down.png')"
                                     click="addThemeBtnClickHandler(event)"/>
            </s:HGroup>
        </s:VGroup>

    </s:HGroup>

    <s:HGroup left="10" right="15" height="42" verticalAlign="middle" bottom="5" gap="{HGAP}">
        <form2:InvalidNotifier id="invalidNotifier"
                               height="30" width="100%"/>

        <button:GrayButton id="cancelBtn"
                           width="250"
                           label="Abbrechen"
                           click="cancel()"/>

        <button:BlackButton id="applyBtn"
                            width="250"
                            label="{modeToApplyBtnTitle(mode)}"
                            enabled="{applyBtnEnabled(mode, audioRecorder.isRecording)}"
                            click="apply()"/>
    </s:HGroup>

    <!--REMOVE MODE-->

    <s:Label id="removeTitleLbl"
             visible="{isRemoveMode}"
             width="100%"
             textAlign="center"
             verticalCenter="-100"
             color="{AppColors.TEXT_DARK}" fontSize="24"
             text="Sind Sie sicher, das Sie entfernen möchten?"/>

    <s:Label id="removeNoteTitleLbl"
             visible="{isRemoveMode}"
             width="100%" verticalCenter="0"
             color="{AppColors.TEXT_DARK}" fontSize="18" fontWeight="bold"
             textAlign="center" verticalAlign="middle"/>

</s:Group>
