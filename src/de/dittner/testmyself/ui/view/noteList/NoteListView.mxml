<?xml version="1.0"?>
<view:SmartView xmlns:fx="http://ns.adobe.com/mxml/2009"
                xmlns:s="library://ns.adobe.com/flex/spark"
                xmlns:core="de.dittner.testmyself.ui.common.view.*"
                xmlns:navigation="de.dittner.testmyself.ui.view.noteList.components.pagination.*"
                xmlns:utils="de.dittner.testmyself.ui.common.utils.*"
                xmlns:view="de.dittner.testmyself.ui.common.view.*"
                xmlns:common="de.dittner.testmyself.ui.view.noteList.components.*"
                xmlns:tag="de.dittner.testmyself.ui.view.noteList.components.tag.*"
                xmlns:mp3="de.dittner.testmyself.ui.common.audio.mp3.*"
                currentState="inactive">

    <fx:Script><![CDATA[
        import de.dittner.async.IAsyncOperation;
        import de.dittner.testmyself.model.domain.vocabulary.VocabularyID;
        import de.dittner.testmyself.ui.common.menu.MenuID;
        import de.dittner.testmyself.ui.common.utils.AppColors;
        import de.dittner.testmyself.ui.common.utils.AppSizes;
        import de.dittner.testmyself.ui.common.view.ViewModelFactory;
        import de.dittner.testmyself.ui.view.noteList.components.PageLayout;
        import de.dittner.testmyself.ui.view.noteList.components.form.FormOperationResult;
        import de.dittner.testmyself.ui.view.noteList.components.toolbar.ToolAction;
        import de.dittner.testmyself.ui.view.noteList.components.toolbar.ToolbarEvent;

        [Bindable]
        public var vm:NoteListVM;

        //----------------------------------------------------------------------------------------------
        //
        //  Methods
        //
        //----------------------------------------------------------------------------------------------

        override protected function activating():void {
            if (viewID == MenuID.WORD)
                vm = ViewModelFactory.instance.wordListVM;
            else if (viewID == MenuID.VERB)
                vm = ViewModelFactory.instance.verbListVM;
            else
                throw new Error("Unknown ViewID: " + viewID);

            vm.lockView();
            toolbar.visible = toolbar.includeInLayout = true;
            toolbar.revert();
        }

        override protected function activate():void {
            vm.unlockView();
            vm.viewActivated(viewID);
            vm.reloadPage();

            toolbar.addNoteBtn.enabled = true;
            toolbar.hideDetailsBtn.enabled = true;
            toolbar.transInvertBtn.enabled = true;
            toolbar.filterBtn.enabled = true;
            toolbar.nextBtn.enabled = true;
            toolbar.prevBtn.enabled = true;

            switch (vm.page.vocabulary.id) {
                case VocabularyID.DE_WORD :
                case VocabularyID.EN_WORD :
                    setCurrentState("word");
                    break;
                case VocabularyID.DE_VERB :
                case VocabularyID.EN_VERB :
                    toolbar.hideDetailsBtn.enabled = false;
                    toolbar.transInvertBtn.enabled = false;
                    setCurrentState("verb");
                    break;
                default :
                    throw Error("Unknown vocabulary: " + vm.page.vocabulary.id);
            }
            toolbar.addEventListener(ToolbarEvent.SELECTED, toolActionSelectedHandler);
            noteList.pageLayout = pageLayoutInfo;
        }

        override protected function deactivate():void {
            toolbar.removeEventListener(ToolbarEvent.SELECTED, toolActionSelectedHandler);
            vm.viewDeactivated();
            setCurrentState("inactive");
        }

        private var needSelectFirstNote:Boolean = false;
        private var needSelectLastNote:Boolean = false;
        private var pageLayoutInfo:PageLayout = new PageLayout();
        private function toolActionSelectedHandler(event:ToolbarEvent):void {
            var op:IAsyncOperation;
            switch (event.toolAction) {
                case(ToolAction.NEXT_NOTE) :
                    if (noteList.dataProvider && noteList.dataProvider.length > 0) {
                        if (!noteList.selectedItem) {
                            vm.selectedNote = noteList.dataProvider[0];
                        }
                        else if (noteList.selectedIndex < noteList.dataProvider.length - 1) {
                            noteList.selectedIndex++;
                        }
                        else if (paginationBar.pageNum < paginationBar.totalPages - 1) {
                            vm.page.number++;
                            needSelectFirstNote = true;
                        }
                    }
                    break;
                case(ToolAction.PREV_NOTE) :
                    if (noteList.dataProvider && noteList.dataProvider.length > 0) {
                        if (!noteList.selectedItem) {
                            noteList.selectedItem = noteList.dataProvider[0];
                        }
                        else if (noteList.selectedIndex > 0) {
                            noteList.selectedIndex--;
                        }
                        else if (paginationBar.pageNum > 0) {
                            vm.page.number--;
                            needSelectLastNote = true;
                        }
                    }
                    break;
                case(ToolAction.CREATE_NOTE) :
                    form.visible = true;
                    op = form.add(vm.page.vocabulary.createNote());
                    op.addCompleteCallback(noteAddedComplete);
                    break;
                case(ToolAction.EDIT_NOTE) :
                    form.visible = true;
                    op = form.edit(noteList.selectedNote);
                    op.addCompleteCallback(noteEditComplete);
                    break;
                case(ToolAction.REMOVE_NOTE) :
                    form.visible = true;
                    op = form.remove(noteList.selectedNote);
                    op.addCompleteCallback(noteRemovedComplete);
                    break;
                case(ToolAction.FILTER) :
                    if (!filter.visible) filter.visible = true;
                    break;
                case(ToolAction.INVERT) :
                    pageLayoutInfo.inverted = !pageLayoutInfo.inverted;
                    noteList.invalidatePageLayout();
                    break;
                case(ToolAction.HIDE_DETAILS) :
                    pageLayoutInfo.showDetails = false;
                    noteList.invalidatePageLayout();
                    break;
                case(ToolAction.SHOW_DETAILS) :
                    pageLayoutInfo.showDetails = true;
                    noteList.invalidatePageLayout();
                    break;
                case(ToolAction.PLAY_AUDIO) :
                    if (vm.selectedNote && vm.selectedNote.hasAudio)
                        vm.selectedNote.loadAndPlayAudioComment();
                    break;
            }
        }

        private function noteAddedComplete(op:IAsyncOperation):void {
            if (op.isSuccess && op.result == FormOperationResult.OK) {
                if (vm.page.selectedTag) vm.page.countAllNotes = true;
                else vm.page.allNotesAmount++;
                vm.reloadPage();
            }
            form.visible = false;
        }

        private function noteRemovedComplete(op:IAsyncOperation):void {
            if (op.isSuccess && op.result == FormOperationResult.OK) {
                vm.page.allNotesAmount--;
                vm.reloadPage();
            }
            form.visible = false;
        }

        private function noteEditComplete(op:IAsyncOperation):void {
            if (op.isSuccess && op.result == FormOperationResult.OK) {
                var selectedItem:* = noteList.selectedItem;
                noteList.selectedItem = null;
                noteList.selectedItem = selectedItem;
            }
            form.visible = false;
        }

        private function pageNumChanged(event:Event):void {
            vm.reloadPage();
        }

        private function filterChangedHandler(event:Event):void {
            if (!isActive) return;
            vm.page.number = 0;
            vm.page.countAllNotes = true;
            vm.reloadPage();
        }

        private function noteList_selectedItemChangeHandler(event:Event):void {
            if (!isActive) return;
            toolbar.editNoteBtn.enabled = noteList.selectedNote != null;
            toolbar.removeNoteBtn.enabled = noteList.selectedNote != null;
            toolbar.playCommentBtn.enabled = noteList.selectedNote != null && noteList.selectedNote.hasAudio;
        }

        private function noteList_dataProviderChangedHandler(event:Event):void {
            if (!noteList.dataProvider || noteList.dataProvider.length == 0) {
                needSelectFirstNote = false;
                needSelectLastNote = false;
                return;
            }

            if (needSelectFirstNote) {
                needSelectFirstNote = false;
                vm.selectedNote = noteList.dataProvider[0];
            }
            else if (needSelectLastNote) {
                needSelectLastNote = false;
                vm.selectedNote = noteList.dataProvider[noteList.dataProvider.length - 1];
            }
        }
        ]]></fx:Script>

    <core:states>
        <s:State name="disabled"/>
        <s:State name="inactive"/>
        <s:State name="word"/>
        <s:State name="verb"/>
    </core:states>

    <utils:BG width="100%" height="100%" color="{AppColors.BG}"/>

    <s:Scroller width="50%" top="0" bottom="{AppSizes.SCREEN_FOOTER_HEIGHT}"
                excludeFrom="inactive"
                horizontalScrollPolicy="off"
                hasFocusableChildren="false">
        <common:NoteList id="noteList"
                         selectedNote="@{vm.selectedNote}"
                         dataProvider="{vm.page.noteColl}"
                         width="100%"
                         contentBackgroundAlpha="0"
                         deselectEnabled="true"
                         itemRenderer="de.dittner.testmyself.ui.view.noteList.components.renderer.NoteRenderer"
                         selectedItemChange="noteList_selectedItemChangeHandler(event)"
                         dataProviderChanged="noteList_dataProviderChangedHandler(event)">
            <common:layout>
                <s:VerticalLayout gap="0" useVirtualLayout="false"/>
            </common:layout>
        </common:NoteList>
    </s:Scroller>

    <common:ExampleList id="exampleList"
                        selectedNote="@{vm.selectedExample}"
                        dataProvider="{noteList.selectedNote.exampleColl}"
                        width="50%" top="-1" bottom="{AppSizes.SCREEN_FOOTER_HEIGHT}"
                        right="0"
                        excludeFrom="inactive"
                        showTitle="false"
                        hasFocusableChildren="false"/>

    <!--footer-->
    <utils:BG width="100%" height="{AppSizes.SCREEN_FOOTER_HEIGHT}" bottom="0" color="{AppColors.SCREEN_FOOTER_BG}"/>

    <s:HGroup width="100%" height="{AppSizes.SCREEN_FOOTER_HEIGHT}" bottom="0"
              verticalAlign="middle"
              opaqueBackground="{AppColors.SCREEN_FOOTER_BG}"
              excludeFrom="inactive">
        <mp3:MP3PlayerComponent id="mp3"
                                visible="{mp3.isPlaying || mp3.isPaused}"
                                width="50%"/>

        <navigation:PaginationBar id="paginationBar"
                                  allNotesAmount="{vm.page.allNotesAmount}"
                                  notesOnCurPage="{vm.page.noteColl.length}"
                                  pageSize="{vm.page.size}"
                                  pageNum="@{vm.page.number}"
                                  height="100%"
                                  pageNumChanged="pageNumChanged(event)"/>
        <s:Spacer width="50%"/>
    </s:HGroup>

    <s:Group width="100%" height="100%"
             excludeFrom="inactive"
             visible="{filter.visible}"
             mouseEnabled="{filter.visible}">
        <s:Rect width="100%" height="100%">
            <s:fill>
                <s:SolidColor color="{AppColors.EDITOR_SHADOW}" alpha=".5"/>
            </s:fill>
        </s:Rect>

        <tag:TagFilter id="filter"
                       selectedTag="@{vm.page.selectedTag}"
                       tags="{vm.page.vocabulary.tagColl}"
                       width="350"
                       visible="false"
                       right="0"
                       height="100%"
                       selectedTagChanged="filterChangedHandler(event)"/>
    </s:Group>

</view:SmartView>
