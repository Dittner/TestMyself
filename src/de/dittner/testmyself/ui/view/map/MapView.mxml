<?xml version="1.0"?>
<view:SmartView xmlns:fx="http://ns.adobe.com/mxml/2009"
                xmlns:s="library://ns.adobe.com/flex/spark"
                xmlns:view="de.dittner.testmyself.ui.common.view.*"
                currentState="de">

    <fx:Script><![CDATA[
        import de.dittner.testmyself.model.domain.language.LanguageID;
        import de.dittner.testmyself.ui.common.view.ViewModelFactory;

        [Bindable]
        public var vm:MapVM;

        override protected function activating():void {
            vm = ViewModelFactory.instance.mapVM;
            vm.lockView();
            currentState = vm.selectedLang && vm.selectedLang.id == LanguageID.EN ? "en" : "de";
        }

        override protected function activate():void {
            vm.unlockView();
            vm.viewActivated(viewInfo);
            toolbar.hide();
            actionMenu.hide();
            setMapInCenter();
        }

        override protected function deactivate():void {
            vm.viewDeactivated();
        }

        private var downPoint:Point;
        private function map_mouseDownHandler(event:MouseEvent):void {
            downPoint = new Point(event.localX - map.x, event.localY - map.y);
        }

        private function map_mouseUpHandler(event:MouseEvent):void {
            downPoint = null;
            if (map.x > 0) map.x = 0;
            else if (map.x < width - map.width) map.x = width - map.width;

            if (map.y > 0) map.y = 0;
            else if (map.y < height - map.height) map.y = height - map.height;
        }

        private function map_mouseMoveHandler(event:MouseEvent):void {
            if (downPoint) {
                map.x = event.localX - downPoint.x;
                map.y = event.localY - downPoint.y;
            }
        }

        private function setMapInCenter():void {
            if (map && width) {
                map.y = 0;
                map.x = width - map.width >> 1;
            }
        }
        ]]></fx:Script>

    <view:states>
        <s:State name="de"/>
        <s:State name="en"/>
    </view:states>

    <s:Group width="100%" height="100%"
             clipAndEnableScrolling="true"
             mouseDown="map_mouseDownHandler(event)"
             mouseOut="map_mouseUpHandler(event)"
             mouseUp="map_mouseUpHandler(event)"
             mouseMove="map_mouseMoveHandler(event)">
        <s:BitmapImage id="map"
                       source.en="@Embed('/assets/gb_map.jpg')"
                       source.de="@Embed('/assets/deutschlandkarte.jpg')"/>
    </s:Group>

</view:SmartView>
