<?xml version="1.0"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009"
         xmlns:s="library://ns.adobe.com/flex/spark"
         xmlns:components2="de.dittner.testmyself.ui.common.audio.components.*"
         xmlns:button="de.dittner.testmyself.ui.common.button.*"
         addedToStage="addedToStageHandler(event)"
         removedFromStage="removedFromStageHandler(event)">
    <fx:Metadata>
        [Event(name="removeCommentClick", type="de.dittner.testmyself.ui.common.audio.event.VoiceCommentEvent")]
    </fx:Metadata>

    <fx:Script><![CDATA[
        import de.dittner.testmyself.model.domain.audioComment.AudioComment;
        import de.dittner.testmyself.ui.common.audio.event.VoiceCommentEvent;
        import de.dittner.testmyself.ui.common.audio.utils.PlayerUtils;
        import de.dittner.testmyself.ui.common.utils.AppColors;

        //--------------------------------------
        //  player
        //--------------------------------------
        private var _player:MP3Player = new MP3Player();
        [Bindable("playerChanged")]
        public function get player():MP3Player {return _player;}
        public function set player(value:MP3Player):void {
            if (_player != value) {
                _player = value;
                dispatchEvent(new Event("playerChanged"));
            }
        }

        [Bindable("commentChanged")]
        public function get hasComment():Boolean {return comment && comment.bytes;}

        //--------------------------------------
        //  soundDuration in sec
        //--------------------------------------
        [Bindable("commentChanged")]
        public function get soundDuration():int {return player.soundDuration;}

        //--------------------------------------
        //  comment
        //--------------------------------------
        [Bindable("commentChanged")]
        public function get comment():AudioComment {return player.comment;}
        public function set comment(value:AudioComment):void {
            if (player.comment != value) {
                player.comment = value;
                updateState();
                dispatchEvent(new Event("commentChanged"));
            }
        }

        //--------------------------------------
        //  removeEnabled
        //--------------------------------------
        [Bindable("removeEnabledChanged")]
        public function get removeRecordEnabled():Boolean {return player.removeRecordEnabled;}
        public function set removeRecordEnabled(value:Boolean):void {
            if (player.removeRecordEnabled != value) {
                player.removeRecordEnabled = value;
                dispatchEvent(new Event("removeEnabledChanged"));
            }
        }

        //--------------------------------------
        //  isPlaying
        //--------------------------------------
        [Bindable("stateChanged")]
        public function get isPlaying():Boolean {return currentState == "playing";}

        //--------------------------------------
        //  isPlaying
        //--------------------------------------
        [Bindable("stateChanged")]
        public function get isStopped():Boolean {return currentState == "stopped";}

        //----------------------------------------------------------------------------------------------
        //
        //  Methods
        //
        //----------------------------------------------------------------------------------------------
        public function play():void {
            player.play();
        }

        public function pause():void {
            player.pause();
        }

        public function stop():void {
            player.stop();
        }

        public function remove():void {
            player.remove();
        }

        public function clear():void {
            player.clear();
        }

        private function updateState(e:Event = null):void {
            if (!comment) currentState = "normal";
            else if (player.curState == player.getPlayingState()) currentState = "playing";
            else if (player.curState == player.getPausedState()) currentState = "paused";
            else currentState = "stopped";
            dispatchEvent(new Event("stateChanged"));
        }

        override protected function updateDisplayList(w:Number, h:Number):void {
            super.updateDisplayList(w, h);
        }

        public function secToStr(sec:int):String {
            return PlayerUtils.convertToHHMMSS(sec);
        }

        private function removeRecord():void {
            player.remove();
        }

        private function addedToStageHandler(event:Event):void {
            player.addEventListener(VoiceCommentEvent.REMOVE_COMMENT_CLICK, commentRemoved);
            player.addEventListener("curStateChanged", updateState);
        }
        private function removedFromStageHandler(event:Event):void {
            player.removeEventListener(VoiceCommentEvent.REMOVE_COMMENT_CLICK, commentRemoved);
            player.removeEventListener("curStateChanged", updateState);
        }

        private function commentRemoved(e:VoiceCommentEvent):void {
            dispatchEvent(e.clone());
        }
        ]]></fx:Script>

    <s:states>
        <s:State name="normal"/>
        <s:State name="stopped"/>
        <s:State name="paused"/>
        <s:State name="playing"/>
    </s:states>

    <s:layout>
        <s:HorizontalLayout gap="5" verticalAlign="middle" paddingLeft="10" paddingRight="10"/>
    </s:layout>

    <button:BitmapButton id="playBtn"
                         upImage="@Embed(source='/assets/voiceRecording/play_btn_up.png')"
                         downImage="@Embed(source='/assets/voiceRecording/play_btn_down.png')"
                         visible="true" visible.playing="false"
                         includeInLayout="true" includeInLayout.playing="false"
                         enabled="true" enabled.normal="false"
                         click="player.play()"/>

    <button:BitmapButton id="pauseBtn"
                         upImage="@Embed(source='/assets/voiceRecording/pause_btn_up.png')"
                         downImage="@Embed(source='/assets/voiceRecording/pause_btn_down.png')"
                         visible="false" visible.playing="true"
                         includeInLayout="false" includeInLayout.playing="true"
                         click="player.pause()"/>

    <s:Label text="{secToStr(player.playbackTime)}"
             fontSize="14"
             color="{AppColors.TEXT_BLACK}"/>

    <components2:RewindingSlider id="rewindingSlider"
                                 width="100%"
                                 height="30"
                                 mouseEnabled.normal="false"
                                 minimum="0"
                                 maximum="{player.soundDuration}"
                                 showDataTip="false"
                                 stepSize="0.0"
                                 value="@{player.playbackTime}"
                                 snapInterval="1"
                                 liveDragging="true"
                                 skinClass="de.dittner.testmyself.ui.common.audio.skins.slider.RewindingSliderSkin"/>

    <s:Label text="{secToStr(player.soundDuration)}"
             text.normal="00:00"
             fontSize="14"
             color="{AppColors.TEXT_BLACK}"/>

    <button:BitmapButton id="removeRecordBtn"
                         upImage="@Embed(source='/assets/voiceRecording/delete_btn_up.png')"
                         downImage="@Embed(source='/assets/voiceRecording/delete_btn_down.png')"
                         top="0"
                         right="10"
                         click="removeRecord()"
                         enabled="{player.removeRecordEnabled}"
                         visible="{player.removeRecordEnabled}"
                         includeInLayout="{player.removeRecordEnabled}"
                         description="Die Audioaufnahme entfernen"/>

</s:Group>
