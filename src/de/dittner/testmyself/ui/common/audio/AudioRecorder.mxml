<?xml version="1.0"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009"
         xmlns:s="library://ns.adobe.com/flex/spark"
         xmlns:mp3="de.dittner.testmyself.ui.common.audio.mp3.*"
         xmlns:button="de.dittner.testmyself.ui.common.button.*"
         xmlns:input="de.dittner.testmyself.ui.common.input.*"
         xmlns:common="de.dittner.testmyself.ui.common.*">

    <fx:Script><![CDATA[
        import de.dittner.async.IAsyncOperation;
        import de.dittner.testmyself.model.domain.audioComment.AudioComment;
        import de.dittner.testmyself.ui.common.audio.event.VoiceCommentEvent;
        import de.dittner.testmyself.ui.common.utils.AppColors;
        import de.dittner.testmyself.utils.FileChooser;
        import de.dittner.testmyself.utils.Values;

        //--------------------------------------
        //  comment
        //--------------------------------------
        [Bindable("isMp3LoadingChanged")]
        public function get hasComment():Boolean {return _comment && _comment.hasBytes;}

        //--------------------------------------
        //  comment
        //--------------------------------------
        private var _comment:AudioComment;
        public function getComment():AudioComment {
            return _comment && _comment.isMp3 && _comment.hasBytes ? _comment : new AudioComment();
        }
        public function setComment(value:AudioComment):void {
            if (_comment != value) {
                _comment = value;
                if (_comment && _comment.hasBytes) {
                    if (value.isMp3) {
                        mp3Player.comment = _comment;
                        showMP3Player();
                    }
                    else {
                        hideMp3Player();
                    }
                }
                else hideMp3Player();
                dispatchEvent(new Event("isMp3LoadingChanged"));
            }
        }

        //--------------------------------------
        //  isMp3Loading
        //--------------------------------------
        private var _isMp3Loading:Boolean = false;
        [Bindable("isMp3LoadingChanged")]
        public function get isMp3Loading():Boolean {return _isMp3Loading;}
        public function set isMp3Loading(value:Boolean):void {
            if (_isMp3Loading != value) {
                _isMp3Loading = value;
                dispatchEvent(new Event("isMp3LoadingChanged"));
            }
        }

        //----------------------------------------------------------------------------------------------
        //
        //  Methods
        //
        //----------------------------------------------------------------------------------------------

        private function hideMp3Player():void {
            mp3Player.visible = false;
        }

        private function showMP3Player():void {
            mp3Player.visible = true;
        }

        public function clear():void {
            clearBytes();
            mp3Player.clear();
            hideMp3Player();
        }

        private function mp3Player_removeCommentClickHandler(event:VoiceCommentEvent):void {
            clearBytes();
            hideMp3Player();
        }

        private function clearBytes():void {
            mp3Player.comment = null;
            _comment = null;
        }

        private static const BROWSE_FILE_FILTERS:Array = [new FileFilter("MP3-file", "*.mp3")];
        private function loadLocalMp3():void {
            var op:IAsyncOperation = FileChooser.browse(BROWSE_FILE_FILTERS);
            op.addCompleteCallback(mp3Browsed);
        }

        private function mp3Browsed(op:IAsyncOperation):void {
            if (op.isSuccess && op.result) {
                var mp3:ByteArray = op.result as ByteArray;
                var ac:AudioComment;
                if (mp3 && mp3.length > 0) {
                    ac = new AudioComment();
                    ac.bytes = mp3;
                    ac.bytes.position = 0;
                    ac.isMp3 = true;
                    setComment(ac);
                }
            }
        }
        ]]></fx:Script>

    <input:MXLabel height="{Values.PT20}"
                   fontSize="{Values.PT15}"
                   mouseEnabled="false"
                   multiline="false"
                   text="{resourceManager.getString('app', 'AudioRecord')}"
                   color="{AppColors.TEXT_CONTROL_TITLE}"/>

    <common:BG percentWidth="100" top="{Values.PT20}" bottom="0"
               fillAlpha="0" borderColor="{AppColors.INPUT_BORDER}" borderWeight="1"/>

    <mp3:MP3PlayerComponent id="mp3Player"
                            left="{Values.PT10}" right="{Values.PT10}"
                            top="{Values.PT20}" bottom="0"
                            visible="false"
                            removeRecordEnabled="true"
                            removeCommentClick="mp3Player_removeCommentClickHandler(event)"/>

    <button:WhiteButton id="loadLocalMp3Btn"
                        verticalCenter="{Values.PT11}"
                        paddingLeft="{Values.PT10}"
                        paddingRight="{Values.PT10}"
                        right="{Values.PT10}"
                        title="{resourceManager.getString('app', 'ChooseMp3File')}"
                        enabled="{!isMp3Loading}"
                        visible="{!mp3Player.visible}"
                        click="loadLocalMp3()"/>
</s:Group>
